use aves;
use testing.unit;

namespace aves.tests;

// Tests for the class aves.List

public class ListTests is TestFixture
{
	public new() { new base("aves.List tests"); }

	// first tests

	private isZeroMod10 = @x = x % 10 == 0;

	public test_First1Empty()
	{
		var list = [];
		Assert.throws(typeof(InvalidStateError), @= list.first());
	}

	public test_First1NonEmpty()
	{
		var list = [1, 2, 3, 4];
		var first = list.first();
		Assert.areEqual(first, 1);
	}

	public test_First2NoMatch()
	{
		var list = [1, 2, 3, 4];
		Assert.throws(typeof(InvalidStateError), @= list.first(isZeroMod10));
	}

	public test_First2Match()
	{
		var list = [1, 0, 3, 10];
		var first = list.first(isZeroMod10);
		Assert.areEqual(first, 0);
	}

	// firstOrNull tests

	public test_FirstOrNull1Empty()
	{
		var list = [];
		var first = list.firstOrNull();
		Assert.isNull(first);
	}

	public test_FirstOrNull1NonEmpty()
	{
		var list = [1, 2, 3, 4];
		var first = list.firstOrNull();
		Assert.areEqual(first, 1);
	}

	public test_FirstOrNull2NoMatch()
	{
		var list = [1, 2, 3, 4];
		var first = list.firstOrNull(isZeroMod10);
		Assert.isNull(first);
	}

	public test_FirstOrNull2Match()
	{
		var list = [1, 0, 3, 10];
		var first = list.firstOrNull(isZeroMod10);
		Assert.areEqual(first, 0);
	}

	// last tests

	public test_Last1Empty()
	{
		var list = [];
		Assert.throws(typeof(InvalidStateError), @= list.last());
	}

	public test_Last1NonEmpty()
	{
		var list = [1, 2, 3, 4];
		var last = list.last();
		Assert.areEqual(last, 4);
	}

	public test_Last2NoMatch()
	{
		var list = [1, 2, 3, 4];
		Assert.throws(typeof(InvalidStateError), @= list.last(isZeroMod10));
	}

	public test_Last2Match()
	{
		var list = [1, 0, 3, 10];
		var last = list.last(isZeroMod10);
		Assert.areEqual(last, 10);
	}

	// lastOrNull tests

	public test_LastOrNull1Empty()
	{
		var list = [];
		var last = list.lastOrNull();
		Assert.isNull(last);
	}

	public test_LastOrNull1NonEmpty()
	{
		var list = [1, 2, 3, 4];
		var last = list.lastOrNull();
		Assert.areEqual(last, 4);
	}

	public test_LastOrNull2NoMatch()
	{
		var list = [1, 2, 3, 4];
		var last = list.lastOrNull(isZeroMod10);
		Assert.isNull(last);
	}

	public test_LastOrNull2Match()
	{
		var list = [1, 0, 2, 10];
		var last = list.lastOrNull(isZeroMod10);
		Assert.areEqual(last, 10);
	}

	// indexOf tests

	public test_IndexOf1Found()
	{
		var list = [0, 1, 2, 0, 1, 2];
		var index = list.indexOf(2);
		Assert.areEqual(index, 2);
	}

	public test_IndexOf1NotFound()
	{
		var list = [0, 1, 2, 0, 1, 2];
		var index = list.indexOf(3);
		Assert.isNull(index);
	}

	public test_IndexOf2Found()
	{
		var list = [0, 1, 2, 0, 1, 2];
		var index1 = list.indexOf(1, 0);
		var index2 = list.indexOf(1, 3);
		Assert.areEqual(index1, 1);
		Assert.areEqual(index2, 4);
	}

	public test_IndexOf2NotFound()
	{
		var list = [0, 1, 2, 0, 1, 2];
		var index1 = list.indexOf(3, 0);
		var index2 = list.indexOf(0, 4);
		Assert.isNull(index1);
		Assert.isNull(index2);
	}

	public test_IndexOf2Invalid()
	{
		var list = [0, 1, 2, 0, 1, 2];
		Assert.throws(typeof(ArgumentRangeError), @= list.indexOf(0, 7));
		Assert.throws(typeof(ArgumentRangeError), @= list.indexOf(0, -1));
		Assert.throws(typeof(TypeError), @= list.indexOf(0, "0"));
	}

	public test_IndexOf3Found()
	{
		var list = [0, 1, 2, 0, 1, 2];
		var index1 = list.indexOf(2, 0, 3);
		var index2 = list.indexOf(2, 3, 3);
		Assert.areEqual(index1, 2);
		Assert.areEqual(index2, 5);
	}

	public test_IndexOf3NotFound()
	{
		var list = [0, 1, 2, 0, 1, 2];
		var index = list.indexOf(1, 2, 2);
		Assert.isNull(index);
	}

	public test_IndexOf3Invalid()
	{
		var list = [0, 1, 2, 0, 1, 2];
		Assert.throws(typeof(ArgumentRangeError), @= list.indexOf(0, 7, 0));
		Assert.throws(typeof(ArgumentRangeError), @= list.indexOf(0, -1, 0));
		Assert.throws(typeof(ArgumentRangeError), @= list.indexOf(0, 0, -1));
		Assert.throws(typeof(ArgumentError), @= list.indexOf(0, 6, 1));
		Assert.throws(typeof(TypeError), @= list.indexOf(0, "0", 0));
		Assert.throws(typeof(TypeError), @= list.indexOf(0, 0, "0"));
	}

	// End indexOf tests

	// slice tests

	public test_Slice1Empty()
	{
		var list = [];
		var slice = list.slice(0);
		Assert.collectionsMatch(slice, [], Assert.areEqual);
	}

	public test_Slice1Valid()
	{
		var list = [0, 1, 2, 3, 4];
		var slice = list.slice(2);
		Assert.collectionsMatch(slice, [2, 3, 4], Assert.areEqual);
	}

	public test_Slice1Invalid()
	{
		var list = [0, 1, 2, 3, 4];
		Assert.throws(typeof(ArgumentRangeError), @= list.slice(6));
		Assert.throws(typeof(ArgumentRangeError), @= list.slice(-1));
	}

	public test_Slice2Empty()
	{
		var list = [0, 1, 2, 3, 4];
		var slice = list.slice(1, 0);
		Assert.collectionsMatch(slice, [], Assert.areEqual);
	}

	public test_Slice2Valid()
	{
		var list = [0, 1, 2, 3, 4];
		var slice = list.slice(1, 3);
		Assert.collectionsMatch(slice, [1, 2, 3], Assert.areEqual);
	}

	public test_Slice2Invalid()
	{
		var list = [0, 1, 2, 3, 4];
		Assert.throws(typeof(ArgumentRangeError), @= list.slice(6, 0));
		Assert.throws(typeof(ArgumentRangeError), @= list.slice(0, -1));
		Assert.throws(typeof(ArgumentError), @= list.slice(5, 1));
	}

	// End slice tests

	// sliceTo tests
	// Remember: the endIndex is exclusive!

	public test_SliceToEmpty()
	{
		var list = [0, 1, 2, 3, 4];
		var slice1 = list.sliceTo(0, 0);
		var slice2 = list.sliceTo(5, 5);
		Assert.collectionsMatch(slice1, [], Assert.areEqual);
		Assert.collectionsMatch(slice2, [], Assert.areEqual);
	}

	public test_SliceToEnd()
	{
		var list = [0, 1, 2, 3, 4];
		var slice = list.sliceTo(3, 5);
		Assert.collectionsMatch(slice, [3, 4], Assert.areEqual);
	}

	public test_SliceToValid()
	{
		var list = [0, 1, 2, 3, 4];
		var slice = list.sliceTo(1, 3);
		Assert.collectionsMatch(slice, [1, 2], Assert.areEqual);
	}

	public test_SliceToInvalid()
	{
		var list = [0, 1, 2, 3, 4];
		Assert.throws(typeof(ArgumentRangeError), @= list.sliceTo(5, 0));
		Assert.throws(typeof(ArgumentRangeError), @= list.sliceTo(0, 6));
		Assert.throws(typeof(TypeError), @= list.sliceTo("0", 0));
		Assert.throws(typeof(TypeError), @= list.sliceTo(0, "0"));
	}

	// End sliceTo tests

	// Iterator tests

	public test_IterEmpty()
	{
		var list = [];
		var iterator = list.iter;
		Assert.isFalse(iterator.moveNext());
	}

	public test_Iter1()
	{
		var list = [1];
		var iterator = list.iter;
		Assert.isTrue(iterator.moveNext());
		Assert.areEqual(iterator.current, 1);
		Assert.isFalse(iterator.moveNext());
	}

	public test_Iter2()
	{
		var list = [1, 2];
		var iterator = list.iter;
		Assert.isTrue(iterator.moveNext());
		Assert.areEqual(iterator.current, 1);
		Assert.isTrue(iterator.moveNext());
		Assert.areEqual(iterator.current, 2);
		Assert.isFalse(iterator.moveNext());
	}

	// End iterator tests
}

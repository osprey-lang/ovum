use aves.*;

namespace math;

/// Summary: Implements a pseudo-random number generator, using the Mersenne Twister algorithm.
///
///          This algorithm is NOT cryptographcially safe and should not be used to encrypt any
///          form of sensitive data.
public class Random
{
	public new()
	{
		init(Env.tickCount);
	}

	public new(seed)
	{
		init(int(seed));
	}

	private state = new BufferView(new Buffer(listSize * 4), BufferViewKind.uint32);
	private index = 0;

	// [0, 2**32 - 1]
	public nextInt()
	{
		return sample().asInt();
	}
	// [0, 2**32 - 1]
	public nextUInt()
	{
		return sample();
	}
	// [0.0, 1.0]
	public nextReal()
	{
		// 4294967295 = uint32 max
		return sample() / 4_294_967_295.0;
	}
	// [false, true]
	public nextBoolean()
	{
		return sample() & 1 == 1;
	}

	private init(seed)
	{
		state[0] = seed;
		var i = 1;
		while i < listSize {
			// Remember: Osprey's Ints are all 64 bits, so this multiplication will never overflow
			state[i] = 0x6c07_8965 * (state[i - 1] ^ (state[i - 1] >> 30)) + i;
			i += 1;
		}
	}

	// Generates a number in the range [0, 2**32 - 1]
	private sample()
	{
		if index == 0 {
			generateNumbers();
		}

		var y = state[index];
		y ^= y >> 11;
		y ^= (y << 7) & 0x9d2c_5680;
		y ^= (y << 15) & 0xefc6_0000;
		y ^= y >> 18;

		index = (index + 1) % listSize;
		return y;
	}

	private generateNumbers()
	{
		const int32SignBit = 0x8000_0000;
		const int32Max     = 0x7fff_ffff;

		var i = 0;
		while i < listSize {
			var y =
				state[i] & int32SignBit +             // Bit 31 of state[i]
				state[(i + 1) % listSize] & int32Max; // Bits 0-30 of "next" state

			state[i] = state[(i + 397) % listSize] ^ (y >> 1);
			if y % 2 != 0 { // y is odd
				state[i] ^= 0x9908_b0df;
			}
			i += 1;
		}
	}

	private const listSize = 624;

	private static _instance = new Random();
	internal static get instance => _instance;
}

public function randomInt()
{
	return Random.instance.nextInt();
}

public function randomUInt()
{
	return Random.instance.nextUInt();
}

public function randomReal()
{
	return Random.instance.nextReal();
}

public function randomBoolean()
{
	return Random.instance.nextBoolean();
}
